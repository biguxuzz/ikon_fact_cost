/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ВыручкаИСебестоимостьПродаж ////////////////////////////////////////////////////////////////////////////////////////// 
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ВыручкаИСебестоимостьПродаж.Период, МЕСЯЦ) КАК Период,
	ВыручкаИСебестоимостьПродаж.Подразделение КАК Подразделение,
	ВЫБОР КОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
			 И ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
			ТОГДА &СтатьяДоходов_ЛокальныеПродажи
			 
		  КОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
			 И НЕ ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион.Ссылка ЕСТЬ NULL
		  	ТОГДА &СтатьяДоходов_Экспорт
		  	
		  ИНАЧЕ &СтатьяДоходов_Прочие
	КОНЕЦ КАК СтатьяДоходов,
	ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион КАК ВГО,
    ВЫБОР КОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
    		ТОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер
    	ИНАЧЕ НЕОПРЕДЕЛЕНО
    КОНЕЦ КАК КонтрагентВГО,
    ВыручкаИСебестоимостьПродаж.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельности,
    ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.Производитель КАК Бренд,
    СУММА(ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл) КАК Сумма
ПОМЕСТИТЬ ВТ_ВыручкаИСебестоимостьПродажДоходы
ИЗ
    РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
ГДЕ
    ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
   		И ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл <> 0   			
СГРУППИРОВАТЬ ПО
    НАЧАЛОПЕРИОДА(ВыручкаИСебестоимостьПродаж.Период, МЕСЯЦ),
	ВыручкаИСебестоимостьПродаж.Подразделение,
	ВЫБОР КОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
			 И ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
			ТОГДА &СтатьяДоходов_ЛокальныеПродажи
			 
		  КОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
			 И НЕ ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион.Ссылка ЕСТЬ NULL
		  	ТОГДА &СтатьяДоходов_Экспорт
		  	
		  ИНАЧЕ &СтатьяДоходов_Прочие
	КОНЕЦ,
	ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион,
    ВЫБОР КОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
    		ТОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер
    	ИНАЧЕ НЕОПРЕДЕЛЕНО
    КОНЕЦ,
    ВыручкаИСебестоимостьПродаж.НаправлениеДеятельностиКонтрагента,
    ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.Производитель
;

ВЫБРАТЬ
    НАЧАЛОПЕРИОДА(ВыручкаИСебестоимостьПродажОбороты.Период, МЕСЯЦ) КАК Период,
    ВыручкаИСебестоимостьПродажОбороты.Подразделение КАК Подразделение,
    СтатьиРасходов.Ссылка КАК СтатьяРасходов,
    ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион КАК ВГО,
    ВЫБОР КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
    		ТОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Партнер
    	ИНАЧЕ НЕОПРЕДЕЛЕНО
    КОНЕЦ КАК КонтрагентВГО,
    ВыручкаИСебестоимостьПродажОбороты.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельности,
    ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.Производитель КАК Бренд,
    МИНИМУМ(ВЫБОР КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
    		ТОГДА &Себестоимость_СчетУчета
    	  ИНАЧЕ ПорядокОтраженияНаСчетахУчета.СчетУчета
   	КОНЕЦ) КАК СчетУчета,
   	СУММА(ВыручкаИСебестоимостьПродажОбороты.СтоимостьРеглОборот
   		+ ВыручкаИСебестоимостьПродажОбороты.ДопРасходыРеглОборот
   		+ ВыручкаИСебестоимостьПродажОбороты.ПостатейныеПостоянныеРеглОборот
   		+ ВыручкаИСебестоимостьПродажОбороты.ПостатейныеПеременныеРеглОборот
   		+ ВыручкаИСебестоимостьПродажОбороты.РасходыНаПродажуРеглОборот
   		+ ВыручкаИСебестоимостьПродажОбороты.ТрудозатратыРеглОборот) КАК Сумма
ПОМЕСТИТЬ ВТ_ВыручкаИСебестоимостьПродажРасходы
ИЗ
    РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&ДатаНачала, &ДатаОкончания, Месяц, ) КАК ВыручкаИСебестоимостьПродажОбороты

		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		ПО СтатьиРасходов.Ссылка = 
			ВЫБОР КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
    				ТОГДА &СтатьяРасходов_Изменение
    			 ИНАЧЕ &СтатьяРасходов_Прочие
    	    КОНЕЦ
    		
   			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
   			ПО СтатьиРасходов.ГруппаФинансовогоУчета = ПорядокОтраженияНаСчетахУчета.АналитикаУчета
ГДЕ
	ВыручкаИСебестоимостьПродажОбороты.СтоимостьРеглОборот
   		+ ВыручкаИСебестоимостьПродажОбороты.ДопРасходыРеглОборот
   		+ ВыручкаИСебестоимостьПродажОбороты.ПостатейныеПостоянныеРеглОборот
   		+ ВыручкаИСебестоимостьПродажОбороты.ПостатейныеПеременныеРеглОборот
   		+ ВыручкаИСебестоимостьПродажОбороты.РасходыНаПродажуРеглОборот
   		+ ВыручкаИСебестоимостьПродажОбороты.ТрудозатратыРеглОборот <> 0
СГРУППИРОВАТЬ ПО
    НАЧАЛОПЕРИОДА(ВыручкаИСебестоимостьПродажОбороты.Период, МЕСЯЦ),
    ВыручкаИСебестоимостьПродажОбороты.Подразделение,
    СтатьиРасходов.Ссылка,
    ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион,
    ВЫБОР КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
    		ТОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Партнер
    	ИНАЧЕ НЕОПРЕДЕЛЕНО
    КОНЕЦ,
    ВыручкаИСебестоимостьПродажОбороты.НаправлениеДеятельностиКонтрагента,
    ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.Производитель
;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ПрочиеРасходы //////////////////////////////////////////////////////////////////////////////////////////////////////// 
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ) КАК Период,
    ПрочиеРасходы.Подразделение КАК Подразделение,
    ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
    ВЫРАЗИТЬ(ПрочиеРасходы.АналитикаУчетаНоменклатуры.Номенклатура.Производитель КАК Справочник.Производители) КАК Производитель,
	ПрочиеРасходы.Регистратор КАК Регистратор,
	ПрочиеРасходы.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
    СУММА(ПрочиеРасходы.СуммаРегл) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыРасход
ИЗ
    РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
ГДЕ
	ПрочиеРасходы.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
		И ПрочиеРасходы.ХозяйственнаяОперация В (&ХозяйственныеОперации)
		И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		И ПрочиеРасходы.СуммаРегл <> 0
СГРУППИРОВАТЬ ПО
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ),
    ПрочиеРасходы.Подразделение,
    ПрочиеРасходы.СтатьяРасходов,
    ВЫРАЗИТЬ(ПрочиеРасходы.АналитикаУчетаНоменклатуры.Номенклатура.Производитель КАК Справочник.Производители),
	ПрочиеРасходы.Регистратор,
	ПрочиеРасходы.ИдентификаторФинЗаписи
ИМЕЮЩИЕ
	СУММА(ПрочиеРасходы.СуммаРегл) <> 0
;

ВЫБРАТЬ
	ПрочиеРасходы.Период КАК Период,
    ПрочиеРасходы.Подразделение КАК Подразделение,
    ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
    СУММА(ПрочиеРасходы.Сумма) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыРасходИтоги
ИЗ
    ВТ_ПрочиеРасходыРасход КАК ПрочиеРасходы
СГРУППИРОВАТЬ ПО
	ПрочиеРасходы.Период,
    ПрочиеРасходы.Подразделение,
    ПрочиеРасходы.СтатьяРасходов
ИМЕЮЩИЕ
	СУММА(ПрочиеРасходы.Сумма) <> 0
;

ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ) КАК Период,
    ПрочиеРасходы.Подразделение КАК Подразделение,
    ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
    ВЫБОР КОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
			ТОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ КАК КонтрагентВГО,
	СУММА(ПрочиеРасходы.СуммаРегл) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыПриходПредварительно
ИЗ
    РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
	
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходыРасходИтоги КАК ПрочиеРасходыРасходИтоги
		ПО НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПрочиеРасходыРасходИтоги.Период, МЕСЯЦ)
			И ПрочиеРасходы.Подразделение = ПрочиеРасходыРасходИтоги.Подразделение
			И ПрочиеРасходы.СтатьяРасходов = ПрочиеРасходыРасходИтоги.СтатьяРасходов
		
			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
			ПО ПрочиеРасходы.Регистратор = ПриобретениеТоваровУслуг.Ссылка	
ГДЕ
	ПрочиеРасходы.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
		И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		И ПрочиеРасходы.СуммаРегл <> 0
СГРУППИРОВАТЬ ПО
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ),
    ПрочиеРасходы.Подразделение,
    ПрочиеРасходы.СтатьяРасходов,
    ВЫБОР КОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
			ТОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ

ОБЪЕДИНИТЬ ВСЕ
ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ) КАК Период,
    ПрочиеРасходы.Подразделение КАК Подразделение,
    ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
    ВЫБОР КОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
			ТОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ КАК КонтрагентВГО,
	СУММА(ПрочиеРасходы.СуммаРегл) КАК Сумма
ИЗ
    РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
	
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходыРасходИтоги КАК ПрочиеРасходыРасходИтоги
		ПО НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПрочиеРасходыРасходИтоги.Период, МЕСЯЦ)
			И ПрочиеРасходы.Подразделение = ПрочиеРасходыРасходИтоги.Подразделение
			И ПрочиеРасходы.СтатьяРасходов = ПрочиеРасходыРасходИтоги.СтатьяРасходов
		
			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Сторно КАК ДокументСторно
			ПО ПрочиеРасходы.Регистратор = ДокументСторно.Ссылка
		
				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
				ПО ДокументСторно.СторнируемыйДокумент = ПриобретениеТоваровУслуг.Ссылка	
ГДЕ
	ПрочиеРасходы.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
		И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		И ПрочиеРасходы.СуммаРегл <> 0
СГРУППИРОВАТЬ ПО
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ),
    ПрочиеРасходы.Подразделение,
    ПрочиеРасходы.СтатьяРасходов,
    ВЫБОР КОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
			ТОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ
;

ВЫБРАТЬ
	ПрочиеРасходы.Период КАК Период,
    ПрочиеРасходы.Подразделение КАК Подразделение,
    ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
    ПрочиеРасходы.КонтрагентВГО КАК КонтрагентВГО,
	СУММА(ПрочиеРасходы.Сумма) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыПриход
ИЗ
  ВТ_ПрочиеРасходыПриходПредварительно КАК ПрочиеРасходы
СГРУППИРОВАТЬ ПО
	ПрочиеРасходы.Период,
    ПрочиеРасходы.Подразделение,
    ПрочиеРасходы.СтатьяРасходов,
    ПрочиеРасходы.КонтрагентВГО
ИМЕЮЩИЕ
	СУММА(ПрочиеРасходы.Сумма) <> 0
;	
	
ВЫБРАТЬ
	ПрочиеРасходы.Период КАК Период,
    ПрочиеРасходы.Подразделение КАК Подразделение,
    ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
    СУММА(ПрочиеРасходы.Сумма) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыПриходИтоги
ИЗ
  ВТ_ПрочиеРасходыПриходПредварительно КАК ПрочиеРасходы
СГРУППИРОВАТЬ ПО
	ПрочиеРасходы.Период,
    ПрочиеРасходы.Подразделение,
    ПрочиеРасходы.СтатьяРасходов
ИМЕЮЩИЕ
	СУММА(ПрочиеРасходы.Сумма) <> 0
;

ВЫБРАТЬ
	ПрочиеРасходыРасход.Период КАК Период,
	ПрочиеРасходыРасход.Подразделение КАК Подразделение,
	ПрочиеРасходыРасход.СтатьяРасходов КАК СтатьяРасходов,
	ПрочиеРасходыРасход.Производитель КАК Производитель,
	ПрочиеРасходыРасход.Регистратор КАК Регистратор,
	ПрочиеРасходыРасход.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
    ПрочиеРасходыПриход.КонтрагентВГО КАК КонтрагентВГО,
	ВЫРАЗИТЬ(ПрочиеРасходыРасход.Сумма * ПрочиеРасходыПриход.Сумма / ПрочиеРасходыПриходИтоги.Сумма КАК ЧИСЛО (20, 2)) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыРасходРаспределено
ИЗ
	ВТ_ПрочиеРасходыРасход КАК ПрочиеРасходыРасход
	
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходыПриход КАК ПрочиеРасходыПриход
        ПО ПрочиеРасходыРасход.Период = ПрочиеРасходыПриход.Период
			И ПрочиеРасходыРасход.Подразделение = ПрочиеРасходыПриход.Подразделение
			И ПрочиеРасходыРасход.СтатьяРасходов = ПрочиеРасходыПриход.СтатьяРасходов
			
  			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходыПриходИтоги КАК ПрочиеРасходыПриходИтоги
			ПО ПрочиеРасходыПриход.Период = ПрочиеРасходыПриходИтоги.Период
				И ПрочиеРасходыПриход.Подразделение = ПрочиеРасходыПриходИтоги.Подразделение
				И ПрочиеРасходыПриход.СтатьяРасходов = ПрочиеРасходыПриходИтоги.СтатьяРасходов
;

ВЫБРАТЬ
	ПрочиеРасходыРасходРаспределено.Период КАК Период,
	ПрочиеРасходыРасходРаспределено.Подразделение КАК Подразделение,
	ПрочиеРасходыРасходРаспределено.СтатьяРасходов КАК СтатьяРасходов,
	ПрочиеРасходыРасходРаспределено.Производитель КАК Производитель,
	ПрочиеРасходыРасходРаспределено.Регистратор КАК Регистратор,
	ПрочиеРасходыРасходРаспределено.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
    СУММА(ПрочиеРасходыРасходРаспределено.Сумма) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыРасходРаспределеноИтоги
ИЗ
	ВТ_ПрочиеРасходыРасходРаспределено КАК ПрочиеРасходыРасходРаспределено
СГРУППИРОВАТЬ ПО			
	ПрочиеРасходыРасходРаспределено.Период,
	ПрочиеРасходыРасходРаспределено.Подразделение,
	ПрочиеРасходыРасходРаспределено.СтатьяРасходов,
	ПрочиеРасходыРасходРаспределено.Производитель,
	ПрочиеРасходыРасходРаспределено.Регистратор,
	ПрочиеРасходыРасходРаспределено.ИдентификаторФинЗаписи		
;

ВЫБРАТЬ
	ПрочиеРасходыРасходРаспределено.Период КАК Период,
	ПрочиеРасходыРасходРаспределено.Подразделение КАК Подразделение,
	ПрочиеРасходыРасходРаспределено.СтатьяРасходов КАК СтатьяРасходов,
	ПрочиеРасходыРасходРаспределено.Производитель КАК Производитель,
	ПрочиеРасходыРасходРаспределено.Регистратор КАК Регистратор,
	ПрочиеРасходыРасходРаспределено.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
    ПрочиеРасходыРасходРаспределено.КонтрагентВГО КАК КонтрагентВГО,
	ПрочиеРасходыРасходРаспределено.Сумма КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыРасходОткорректировано
ИЗ
	ВТ_ПрочиеРасходыРасходРаспределено КАК ПрочиеРасходыРасходРаспределено
ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ПрочиеРасходыРасход.Период КАК Период,
	ПрочиеРасходыРасход.Подразделение КАК Подразделение,
	ПрочиеРасходыРасход.СтатьяРасходов КАК СтатьяРасходов,
	ПрочиеРасходыРасход.Производитель КАК Производитель,
	ПрочиеРасходыРасход.Регистратор КАК Регистратор,
	ПрочиеРасходыРасход.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	НЕОПРЕДЕЛЕНО КАК КонтрагентВГО,
    ПрочиеРасходыРасход.Сумма - ЕСТЬNULL(ПрочиеРасходыРасходРаспределеноИтоги.Сумма, 0) КАК Сумма
ИЗ
	ВТ_ПрочиеРасходыРасход КАК ПрочиеРасходыРасход
	
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходыРасходРаспределеноИтоги КАК ПрочиеРасходыРасходРаспределеноИтоги
		ПО ПрочиеРасходыРасход.Период = ПрочиеРасходыРасходРаспределеноИтоги.Период
			И ПрочиеРасходыРасход.Подразделение = ПрочиеРасходыРасходРаспределеноИтоги.Подразделение
			И ПрочиеРасходыРасход.СтатьяРасходов = ПрочиеРасходыРасходРаспределеноИтоги.СтатьяРасходов
			И ВЫБОР КОГДА ПрочиеРасходыРасход.Производитель.Ссылка ЕСТЬ NULL И ПрочиеРасходыРасходРаспределеноИтоги.Производитель.Ссылка ЕСТЬ NULL
						ТОГДА ИСТИНА
					ИНАЧЕ ПрочиеРасходыРасход.Производитель = ПрочиеРасходыРасходРаспределеноИтоги.Производитель
			  КОНЕЦ
			И ПрочиеРасходыРасход.Регистратор = ПрочиеРасходыРасходРаспределеноИтоги.Регистратор
			И ПрочиеРасходыРасход.ИдентификаторФинЗаписи = ПрочиеРасходыРасходРаспределеноИтоги.ИдентификаторФинЗаписи
ГДЕ
	ПрочиеРасходыРасход.Сумма - ЕСТЬNULL(ПрочиеРасходыРасходРаспределеноИтоги.Сумма, 0) <> 0
;

ВЫБРАТЬ
	ПрочиеРасходыРасходОткорректировано.Период КАК Период,
	ПрочиеРасходыРасходОткорректировано.Подразделение КАК Подразделение,
	ПрочиеРасходыРасходОткорректировано.СтатьяРасходов КАК СтатьяРасходов,
	ПрочиеРасходыРасходОткорректировано.Производитель КАК Производитель,
	ПрочиеРасходыРасходОткорректировано.Регистратор КАК Регистратор,
	ПрочиеРасходыРасходОткорректировано.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	ВЫБОР КОГДА ВЫРАЗИТЬ(ПрочиеРасходыРасходОткорректировано.КонтрагентВГО КАК Справочник.Партнеры).Ссылка ЕСТЬ NULL
			ТОГДА &БизнесРегион_Внешний
		ИНАЧЕ ВЫРАЗИТЬ(ПрочиеРасходыРасходОткорректировано.КонтрагентВГО КАК Справочник.Партнеры).БизнесРегион
	КОНЕЦ КАК ВГО,		
    ПрочиеРасходыРасходОткорректировано.КонтрагентВГО КАК КонтрагентВГО,
	СУММА(ПрочиеРасходыРасходОткорректировано.Сумма) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходы 
ИЗ
	ВТ_ПрочиеРасходыРасходОткорректировано КАК ПрочиеРасходыРасходОткорректировано
СГРУППИРОВАТЬ ПО
	ПрочиеРасходыРасходОткорректировано.Период,
	ПрочиеРасходыРасходОткорректировано.Подразделение,
	ПрочиеРасходыРасходОткорректировано.СтатьяРасходов,
	ПрочиеРасходыРасходОткорректировано.Производитель,
	ПрочиеРасходыРасходОткорректировано.Регистратор,
	ПрочиеРасходыРасходОткорректировано.ИдентификаторФинЗаписи,
    ВЫБОР КОГДА ВЫРАЗИТЬ(ПрочиеРасходыРасходОткорректировано.КонтрагентВГО КАК Справочник.Партнеры).Ссылка ЕСТЬ NULL
			ТОГДА НЕОПРЕДЕЛЕНО
		ИНАЧЕ ВЫРАЗИТЬ(ПрочиеРасходыРасходОткорректировано.КонтрагентВГО КАК Справочник.Партнеры).БизнесРегион
	КОНЕЦ,
	ПрочиеРасходыРасходОткорректировано.КонтрагентВГО
ИМЕЮЩИЕ
	СУММА(ПрочиеРасходыРасходОткорректировано.Сумма) <> 0	
;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Линии PL ///////////////////////////////////////////////////////////////////////////////////////////////////////////// 
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ВЫБРАТЬ РАЗЛИЧНЫЕ
	ВыручкаИСебестоимостьПродаж.Подразделение КАК Подразделение,
	ВыручкаИСебестоимостьПродаж.СтатьяРасходов КАК СтатьяРасходов
ПОМЕСТИТЬ ВТ_СтатьиРасходовПодразделения
ИЗ
	ВТ_ВыручкаИСебестоимостьПродажРасходы КАК ВыручкаИСебестоимостьПродаж
ОБЪЕДИНИТЬ
ВЫБРАТЬ
	ПрочиеРасходы.Подразделение КАК Подразделение,
    ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов
ИЗ
	ВТ_ПрочиеРасходы КАК ПрочиеРасходы	
;

ВЫБРАТЬ РАЗЛИЧНЫЕ
	1 КАК Приоритет,
	СтатьиРасходовПодразделения.Подразделение КАК Подразделение,
	СтатьиРасходовПодразделения.СтатьяРасходов КАК СтатьяРасходов,
	СтатьиРасходовПодразделения.СтатьяРасходов КАК СтатьяРасходовОтбор	
ПОМЕСТИТЬ ВТ_СтатьиРасходовПодразделенияПриоритеты
ИЗ
	ВТ_СтатьиРасходовПодразделения КАК СтатьиРасходовПодразделения
ОБЪЕДИНИТЬ	
ВЫБРАТЬ
	2 КАК Приоритет,
	СтатьиРасходовПодразделения.Подразделение КАК Подразделение,
	СтатьиРасходовПодразделения.СтатьяРасходов КАК СтатьяРасходов,
	СтатьиРасходовПодразделения.СтатьяРасходов.Родитель КАК СтатьяРасходовОтбор
ИЗ
	ВТ_СтатьиРасходовПодразделения КАК СтатьиРасходовПодразделения
ОБЪЕДИНИТЬ	
ВЫБРАТЬ
	3 КАК Приоритет,
	СтатьиРасходовПодразделения.Подразделение КАК Подразделение,
	СтатьиРасходовПодразделения.СтатьяРасходов КАК СтатьяРасходов,
	СтатьиРасходовПодразделения.СтатьяРасходов.Родитель.Родитель КАК СтатьяРасходовОтбор
ИЗ
	ВТ_СтатьиРасходовПодразделения КАК СтатьиРасходовПодразделения
;

ВЫБРАТЬ
	СтатьиРасходовПодразделенияПриоритеты.Подразделение КАК Подразделение,
	СтатьиРасходовПодразделенияПриоритеты.СтатьяРасходов КАК СтатьяРасходов,
	СтатьиРасходовПодразделенияПриоритеты.Приоритет КАК Приоритет,
	ikon_ЛинииPLСтатейРасходов.ЛинияPL КАК ЛинияPL
ПОМЕСТИТЬ ВТ_ЛинииPLСтатейРасходовПриоритеты
ИЗ
	ВТ_СтатьиРасходовПодразделенияПриоритеты КАК СтатьиРасходовПодразделенияПриоритеты
	
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ikon_ЛинииPLСтатейРасходов КАК ikon_ЛинииPLСтатейРасходов
		ПО СтатьиРасходовПодразделенияПриоритеты.Подразделение = ikon_ЛинииPLСтатейРасходов.Подразделение
			И СтатьиРасходовПодразделенияПриоритеты.СтатьяРасходовОтбор = ikon_ЛинииPLСтатейРасходов.СтатьяРасходов
;

ВЫБРАТЬ
	ЛинииPLСтатейРасходовПриоритеты.Подразделение КАК Подразделение,
	ЛинииPLСтатейРасходовПриоритеты.СтатьяРасходов КАК СтатьяРасходов,
	МИНИМУМ(ЛинииPLСтатейРасходовПриоритеты.Приоритет) КАК Приоритет
ПОМЕСТИТЬ ВТ_ЛинииPLСтатейРасходовМинимальныеПриоритеты
ИЗ
	ВТ_ЛинииPLСтатейРасходовПриоритеты КАК ЛинииPLСтатейРасходовПриоритеты
СГРУППИРОВАТЬ ПО
	ЛинииPLСтатейРасходовПриоритеты.Подразделение,
	ЛинииPLСтатейРасходовПриоритеты.СтатьяРасходов
;

ВЫБРАТЬ
	ЛинииPLСтатейРасходовПриоритеты.Подразделение КАК Подразделение,
	ЛинииPLСтатейРасходовПриоритеты.СтатьяРасходов КАК СтатьяРасходов,
	МАКСИМУМ(ЛинииPLСтатейРасходовПриоритеты.ЛинияPL) КАК ЛинияPL
ПОМЕСТИТЬ ВТ_ЛинииPLСтатейРасходов
ИЗ
	ВТ_ЛинииPLСтатейРасходовПриоритеты КАК ЛинииPLСтатейРасходовПриоритеты
	
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЛинииPLСтатейРасходовМинимальныеПриоритеты КАК ЛинииPLСтатейРасходовМинимальныеПриоритеты
        ПО ЛинииPLСтатейРасходовПриоритеты.Подразделение = ЛинииPLСтатейРасходовМинимальныеПриоритеты.Подразделение
			И ЛинииPLСтатейРасходовПриоритеты.СтатьяРасходов = ЛинииPLСтатейРасходовМинимальныеПриоритеты.СтатьяРасходов
			И ЛинииPLСтатейРасходовПриоритеты.Приоритет = ЛинииPLСтатейРасходовМинимальныеПриоритеты.Приоритет
СГРУППИРОВАТЬ ПО
	ЛинииPLСтатейРасходовПриоритеты.Подразделение,
	ЛинииPLСтатейРасходовПриоритеты.СтатьяРасходов
;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ПрочиеДоходы И ПрочиеРасходы ///////////////////////////////////////////////////////////////////////////////////////// 
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ПрочиеДоходы.Период, МЕСЯЦ) КАК Период,
	ПрочиеДоходы.Подразделение КАК Подразделение,
    ПрочиеДоходы.Подразделение.Код КАК ПодразделениеКод,
    ПрочиеДоходы.СтатьяДоходов КАК СтатьяРасходов,
    ПрочиеДоходы.СтатьяДоходов.Код КАК СтатьяРасходовКод,
    ПрочиеДоходы.СтатьяДоходов КАК СтатьяЗатрат,
    ПрочиеДоходы.СтатьяДоходов.Код КАК СтатьяЗатратКод,
    ПрочиеДоходы.СтатьяДоходов.Родитель КАК СтатьяБюджета,
    ПрочиеДоходы.СтатьяДоходов.Родитель.Код КАК СтатьяБюджетаКод,
    ПрочиеДоходы.СтатьяДоходов.ikon_ЛинияPL КАК ЛинияPL,
    ПрочиеДоходы.СтатьяДоходов.ikon_ЛинияPL.Код КАК ЛинияPLКод,
    НЕОПРЕДЕЛЕНО КАК ВГО,
    НЕОПРЕДЕЛЕНО КАК КонтрагентВГО,
    ВЫРАЗИТЬ(ПрочиеДоходы.АналитикаДоходов КАК Справочник.Номенклатура).Производитель КАК Бренд,
	ПрочиеДоходы.Регистратор КАК Регистратор,
	ПрочиеДоходы.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	ПрочиеДоходы.СтатьяДоходов.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	ПрочиеДоходы.СуммаРегл КАК Сумма,
	ПрочиеДоходы.СуммаРегл КАК СуммаФР
ПОМЕСТИТЬ ВТ_ПрочиеДоходыРасходы
ИЗ
    РегистрНакопления.ПрочиеДоходы КАК ПрочиеДоходы   	
ГДЕ
	ПрочиеДоходы.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
		И ПрочиеДоходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		И ПрочиеДоходы.СуммаРегл <> 0
    
ОБЪЕДИНИТЬ ВСЕ
ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ) КАК Период,
    ПрочиеРасходы.Подразделение КАК Подразделение,
    ПрочиеРасходы.Подразделение.Код КАК ПодразделениеКод,
    ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
    ПрочиеРасходы.СтатьяРасходов.Код КАК СтатьяРасходовКод,
    ПрочиеРасходы.СтатьяРасходов.Родитель КАК СтатьяЗатрат,
    ПрочиеРасходы.СтатьяРасходов.Родитель.Код КАК СтатьяЗатратКод,
    ПрочиеРасходы.СтатьяРасходов.Родитель.Родитель КАК СтатьяБюджета,
    ПрочиеРасходы.СтатьяРасходов.Родитель.Родитель.Код КАК СтатьяБюджетаКод,
    ЛинииPLСтатейРасходов.ЛинияPL КАК ЛинияPL,
    ЛинииPLСтатейРасходов.ЛинияPL.Код КАК ЛинияPLКод,
    ПрочиеРасходы.ВГО КАК ВГО,
    ПрочиеРасходы.КонтрагентВГО КАК КонтрагентВГО,
    ПрочиеРасходы.Производитель КАК Производитель,
    ПрочиеРасходы.Регистратор КАК Регистратор,
	ПрочиеРасходы.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	ЕСТЬNULL(ikon_ПравилаПерекрестнойПроверки.ГФУ, ПрочиеРасходы.СтатьяРасходов.ГруппаФинансовогоУчета) КАК ГруппаФинансовогоУчета,
	ПрочиеРасходы.Сумма КАК Сумма,
	-ПрочиеРасходы.Сумма КАК СуммаФР
ИЗ
    ВТ_ПрочиеРасходы КАК ПрочиеРасходы
    	
    	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЛинииPLСтатейРасходов КАК ЛинииPLСтатейРасходов
    	ПО ПрочиеРасходы.Подразделение = ЛинииPLСтатейРасходов.Подразделение
    		И ПрочиеРасходы.СтатьяРасходов = ЛинииPLСтатейРасходов.СтатьяРасходов
    		
    		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ikon_ПравилаПерекрестнойПроверки КАК ikon_ПравилаПерекрестнойПроверки
    		ПО ПрочиеРасходы.Подразделение = ikon_ПравилаПерекрестнойПроверки.Подразделения
    			И ПрочиеРасходы.СтатьяРасходов = ikon_ПравилаПерекрестнойПроверки.СтатьяРасходов
;    

ВЫБРАТЬ РАЗЛИЧНЫЕ
	ПрочиеДоходыРасходы.Регистратор КАК Регистратор,
	ПрочиеДоходыРасходы.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи
ПОМЕСТИТЬ ВТ_РегистраторыИдентификаторы
ИЗ
	ВТ_ПрочиеДоходыРасходы КАК ПрочиеДоходыРасходы	
;

ВЫБРАТЬ
	ДвиженияПоПрочимАктивамПассивам.Регистратор КАК Регистратор,
	ДвиженияПоПрочимАктивамПассивам.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
    МАКСИМУМ(ДвиженияПоПрочимАктивамПассивам.НаправлениеДеятельности) КАК НаправлениеДеятельности
ПОМЕСТИТЬ ВТ_ДвиженияПоПрочимАктивамПассивам
ИЗ
	ВТ_РегистраторыИдентификаторы КАК РегистраторыИдентификаторы
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК ДвиженияПоПрочимАктивамПассивам
		ПО РегистраторыИдентификаторы.Регистратор = ДвиженияПоПрочимАктивамПассивам.Регистратор 
			И РегистраторыИдентификаторы.ИдентификаторФинЗаписи = ДвиженияПоПрочимАктивамПассивам.ИдентификаторФинЗаписи
СГРУППИРОВАТЬ ПО
	ДвиженияПоПрочимАктивамПассивам.Регистратор,
	ДвиженияПоПрочимАктивамПассивам.ИдентификаторФинЗаписи
;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Результат //////////////////////////////////////////////////////////////////////////////////////////////////////////// 
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ВЫБРАТЬ
	"Выручка и себестоимость продаж (Доходы)" КАК РегистрИсточник,
	ВыручкаИСебестоимостьПродаж.Период КАК Период,
	ВыручкаИСебестоимостьПродаж.Подразделение КАК Подразделение,
	ВыручкаИСебестоимостьПродаж.Подразделение.Код КАК ПодразделениеКод,
    СтатьиДоходов.Ссылка КАК СтатьяРасходов,
	СтатьиДоходов.Код КАК СтатьяРасходовКод,
	СтатьиДоходов.Ссылка КАК СтатьяЗатрат,
	СтатьиДоходов.Код КАК СтатьяЗатратКод,
	СтатьиДоходов.Родитель КАК СтатьяБюджета,
	СтатьиДоходов.Родитель.Код КАК СтатьяБюджетаКод,
	ВЫБОР КОГДА ВыручкаИСебестоимостьПродаж.ВГО В (&БизнесРегионы_ВГО)
		И НЕ СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL 
			ТОГДА СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО
		ИНАЧЕ СтатьиДоходов.ikon_ЛинияPL
	КОНЕЦ КАК СтрокаОФР,
	ВЫБОР КОГДА ВыручкаИСебестоимостьПродаж.ВГО В (&БизнесРегионы_ВГО)
		И НЕ СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL 
			ТОГДА СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Код
		ИНАЧЕ СтатьиДоходов.ikon_ЛинияPL.Код
	КОНЕЦ КАК СтрокаОФРКод,
	ВыручкаИСебестоимостьПродаж.ВГО КАК ВГО,
    ВыручкаИСебестоимостьПродаж.КонтрагентВГО КАК КонтрагентВГО,
    ВыручкаИСебестоимостьПродаж.НаправлениеДеятельности КАК НаправлениеДеятельности,
    ВыручкаИСебестоимостьПродаж.Бренд КАК Бренд,
    МИНИМУМ(ПорядокОтраженияНаСчетахУчета.СчетУчета) КАК СчетУчета,
    СУММА(ВыручкаИСебестоимостьПродаж.Сумма) КАК Сумма,
	СУММА(ВыручкаИСебестоимостьПродаж.Сумма) КАК СуммаФР
ИЗ
    ВТ_ВыручкаИСебестоимостьПродажДоходы КАК ВыручкаИСебестоимостьПродаж
    	
    	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиДоходов КАК СтатьиДоходов
    	ПО ВыручкаИСебестоимостьПродаж.СтатьяДоходов = СтатьиДоходов.Ссылка
    	
    	    ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
    		ПО СтатьиДоходов.ГруппаФинансовогоУчета = ПорядокОтраженияНаСчетахУчета.АналитикаУчета
СГРУППИРОВАТЬ ПО
	ВыручкаИСебестоимостьПродаж.Период,
	ВыручкаИСебестоимостьПродаж.Подразделение,
    СтатьиДоходов.Ссылка,
	ВЫБОР КОГДА ВыручкаИСебестоимостьПродаж.ВГО В (&БизнесРегионы_ВГО)
		И НЕ СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО
		ИНАЧЕ СтатьиДоходов.ikon_ЛинияPL
	КОНЕЦ,
	ВЫБОР КОГДА ВыручкаИСебестоимостьПродаж.ВГО В (&БизнесРегионы_ВГО)
		И НЕ СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Код
		ИНАЧЕ СтатьиДоходов.ikon_ЛинияPL.Код
	КОНЕЦ,
	ВыручкаИСебестоимостьПродаж.ВГО,
    ВыручкаИСебестоимостьПродаж.КонтрагентВГО,
    ВыручкаИСебестоимостьПродаж.НаправлениеДеятельности,
    ВыручкаИСебестоимостьПродаж.Бренд
    
ОБЪЕДИНИТЬ ВСЕ
ВЫБРАТЬ
    "Выручка и себестоимость продаж (Расходы)" КАК РегистрИсточник,
	ВыручкаИСебестоимостьПродаж.Период КАК Период,
    ВыручкаИСебестоимостьПродаж.Подразделение КАК Подразделение,
    ВыручкаИСебестоимостьПродаж.Подразделение.Код КАК ПодразделениеКод,
    ВыручкаИСебестоимостьПродаж.СтатьяРасходов КАК СтатьяРасходов,
    ВыручкаИСебестоимостьПродаж.СтатьяРасходов.Код КАК СтатьяРасходовКод,
    ВыручкаИСебестоимостьПродаж.СтатьяРасходов.Родитель КАК СтатьяЗатрат,
    ВыручкаИСебестоимостьПродаж.СтатьяРасходов.Родитель.Код КАК СтатьяЗатратКод,
    ВыручкаИСебестоимостьПродаж.СтатьяРасходов.Родитель.Родитель КАК СтатьяБюджета,
    ВыручкаИСебестоимостьПродаж.СтатьяРасходов.Родитель.Родитель.Код КАК СтатьяБюджетаКод,
    ВЫБОР КОГДА ВыручкаИСебестоимостьПродаж.ВГО В (&БизнесРегионы_ВГО)
		И НЕ ЛинииPLСтатейРасходов.ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
    		ТОГДА ЛинииPLСтатейРасходов.ЛинияPL.ЛинияВГО
    	ИНАЧЕ ЛинииPLСтатейРасходов.ЛинияPL
    КОНЕЦ КАК СтрокаОФР,
    ВЫБОР КОГДА ВыручкаИСебестоимостьПродаж.ВГО В (&БизнесРегионы_ВГО)
		И НЕ ЛинииPLСтатейРасходов.ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
    		ТОГДА ЛинииPLСтатейРасходов.ЛинияPL.ЛинияВГО.Код
    	ИНАЧЕ ЛинииPLСтатейРасходов.ЛинияPL.Код
    КОНЕЦ КАК СтрокаОФРКод,
    ВыручкаИСебестоимостьПродаж.ВГО КАК ВГО,
    ВыручкаИСебестоимостьПродаж.КонтрагентВГО КАК КонтрагентВГО,
    ВыручкаИСебестоимостьПродаж.НаправлениеДеятельности КАК НаправлениеДеятельности,
    ВыручкаИСебестоимостьПродаж.Бренд КАК Бренд,
    ВыручкаИСебестоимостьПродаж.СчетУчета КАК СчетУчета,
   	ВыручкаИСебестоимостьПродаж.Сумма КАК Сумма,
	-ВыручкаИСебестоимостьПродаж.Сумма КАК СуммаФР
ИЗ
    ВТ_ВыручкаИСебестоимостьПродажРасходы КАК ВыручкаИСебестоимостьПродаж

   		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЛинииPLСтатейРасходов КАК ЛинииPLСтатейРасходов
   		ПО ВыручкаИСебестоимостьПродаж.Подразделение = ЛинииPLСтатейРасходов.Подразделение
   			И ВыручкаИСебестоимостьПродаж.СтатьяРасходов = ЛинииPLСтатейРасходов.СтатьяРасходов
   	
ОБЪЕДИНИТЬ ВСЕ
ВЫБРАТЬ
	"Прочие доходы / Прочие расходы" КАК РегистрИсточник,
	ПрочиеДоходыРасходы.Период КАК Период,
	ПрочиеДоходыРасходы.Подразделение КАК Подразделение,
    ПрочиеДоходыРасходы.ПодразделениеКод КАК ПодразделениеКод,
    ПрочиеДоходыРасходы.СтатьяРасходов КАК СтатьяРасходов,
    ПрочиеДоходыРасходы.СтатьяРасходовКод КАК СтатьяРасходовКод,
    ПрочиеДоходыРасходы.СтатьяЗатрат КАК СтатьяЗатрат,
    ПрочиеДоходыРасходы.СтатьяЗатратКод КАК СтатьяЗатратКод,
    ПрочиеДоходыРасходы.СтатьяБюджета КАК СтатьяБюджета,
    ПрочиеДоходыРасходы.СтатьяБюджетаКод КАК СтатьяБюджетаКод,
    ПрочиеДоходыРасходы.ЛинияPL КАК СтрокаОФР,
    ПрочиеДоходыРасходы.ЛинияPLКод КАК СтрокаОФРКод,
    ПрочиеДоходыРасходы.ВГО КАК ВГО,
    ПрочиеДоходыРасходы.КонтрагентВГО КАК КонтрагентВГО,
    ДвиженияПоПрочимАктивамПассивам.НаправлениеДеятельности КАК НаправлениеДеятельности,
    ПрочиеДоходыРасходы.Бренд КАК Бренд,
    МИНИМУМ(ПорядокОтраженияНаСчетахУчета.СчетУчета) КАК СчетУчета,
    СУММА(ПрочиеДоходыРасходы.Сумма) КАК Сумма,
	СУММА(ПрочиеДоходыРасходы.СуммаФР) КАК СуммаФР
ИЗ
    ВТ_ПрочиеДоходыРасходы КАК ПрочиеДоходыРасходы
    	
    	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДвиженияПоПрочимАктивамПассивам КАК ДвиженияПоПрочимАктивамПассивам
    	ПО ПрочиеДоходыРасходы.Регистратор = ДвиженияПоПрочимАктивамПассивам.Регистратор
			И ПрочиеДоходыРасходы.ИдентификаторФинЗаписи = ДвиженияПоПрочимАктивамПассивам.ИдентификаторФинЗаписи
			
			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
    		ПО ПрочиеДоходыРасходы.ГруппаФинансовогоУчета = ПорядокОтраженияНаСчетахУчета.АналитикаУчета
	
СГРУППИРОВАТЬ ПО
	ПрочиеДоходыРасходы.Период,
	ПрочиеДоходыРасходы.Подразделение,
    ПрочиеДоходыРасходы.ПодразделениеКод,
    ПрочиеДоходыРасходы.СтатьяРасходов,
    ПрочиеДоходыРасходы.СтатьяРасходовКод,
    ПрочиеДоходыРасходы.СтатьяЗатрат,
    ПрочиеДоходыРасходы.СтатьяЗатратКод,
    ПрочиеДоходыРасходы.СтатьяБюджета,
    ПрочиеДоходыРасходы.СтатьяБюджетаКод,
    ПрочиеДоходыРасходы.ЛинияPL,
    ПрочиеДоходыРасходы.ЛинияPLКод,
    ПрочиеДоходыРасходы.ВГО,
    ПрочиеДоходыРасходы.КонтрагентВГО,
    ДвиженияПоПрочимАктивамПассивам.НаправлениеДеятельности,
    ПрочиеДоходыРасходы.Бренд
	
ОБЪЕДИНИТЬ ВСЕ
ВЫБРАТЬ
	"Прочая выручка" КАК РегистрИсточник, 
    ПрочаяВыручкаОбороты.Период КАК Период,
    ПрочаяВыручкаОбороты.Подразделение КАК Подразделение,
    ПрочаяВыручкаОбороты.Подразделение.Код КАК ПодразделениеКод,
    ПрочаяВыручкаОбороты.СтатьяДоходов КАК СтатьяРасходов,
    ПрочаяВыручкаОбороты.СтатьяДоходов.Код КАК СтатьяРасходовКод,
    ПрочаяВыручкаОбороты.СтатьяДоходов КАК СтатьяЗатрат,
    ПрочаяВыручкаОбороты.СтатьяДоходов.Код КАК СтатьяЗатратКод,
    ПрочаяВыручкаОбороты.СтатьяДоходов.Родитель КАК СтатьяБюджета,
    ПрочаяВыручкаОбороты.СтатьяДоходов.Родитель.Код КАК СтатьяБюджетаКод,
    ВЫБОР КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
		И НЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
    		ТОГДА ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО
		ИНАЧЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL
	КОНЕЦ КАК СтрокаОФР,
	ВЫБОР КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
		И НЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
    		ТОГДА ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Код
		ИНАЧЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.Код
	КОНЕЦ КАК СтрокаОФРКод,
	ПрочаяВыручкаОбороты.Партнер.БизнесРегион КАК ВГО,
    ВЫБОР КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
    		ТОГДА ПрочаяВыручкаОбороты.Партнер
    	ИНАЧЕ НЕОПРЕДЕЛЕНО
    КОНЕЦ КАК КонтрагентВГО,
    ПрочаяВыручкаОбороты.НаправлениеДеятельности КАК НаправлениеДеятельности,
    ВЫРАЗИТЬ(ПрочаяВыручкаОбороты.АналитикаДоходов КАК Справочник.Номенклатура).Производитель КАК Бренд, 
    МИНИМУМ(ПорядокОтраженияНаСчетахУчета.СчетУчета) КАК СчетУчета,
    СУММА(ПрочаяВыручкаОбороты.ВыручкаБезНДСРеглОборот) КАК Сумма,
	СУММА(ПрочаяВыручкаОбороты.ВыручкаБезНДСРеглОборот) КАК СуммаФР
ИЗ
    РегистрНакопления.ПрочаяВыручка.Обороты(&ДатаНачала, &ДатаОкончания, Месяц, ) КАК ПрочаяВыручкаОбороты

    	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
    	ПО ПрочаяВыручкаОбороты.СтатьяДоходов.ГруппаФинансовогоУчета = ПорядокОтраженияНаСчетахУчета.АналитикаУчета
ГДЕ
	ПрочаяВыручкаОбороты.ВыручкаБезНДСРеглОборот <> 0	
СГРУППИРОВАТЬ ПО
    ПрочаяВыручкаОбороты.Период,
    ПрочаяВыручкаОбороты.Подразделение,
    ПрочаяВыручкаОбороты.СтатьяДоходов,
    ВЫБОР КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
		И НЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
    		ТОГДА ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО
		ИНАЧЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL
	КОНЕЦ,
	ВЫБОР КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
		И НЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
    		ТОГДА ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Код
		ИНАЧЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.Код
	КОНЕЦ,
	ПрочаяВыручкаОбороты.Партнер.БизнесРегион,
    ВЫБОР КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион В (&БизнесРегионы_ВГО)
    		ТОГДА ПрочаяВыручкаОбороты.Партнер
    	ИНАЧЕ НЕОПРЕДЕЛЕНО
    КОНЕЦ,
    ПрочаяВыручкаОбороты.НаправлениеДеятельности,
    ВЫРАЗИТЬ(ПрочаяВыручкаОбороты.АналитикаДоходов КАК Справочник.Номенклатура).Производитель
    