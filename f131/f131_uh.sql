/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ВыручкаИСебестоимостьПродаж ////////////////////////////////////////////////////////////////////////////////////////// 
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ВыручкаИСебестоимостьПродаж.Период, МЕСЯЦ) КАК Период,
	ВыручкаИСебестоимостьПродаж.Подразделение КАК Подразделение,
	СтатьиДоходов.Ссылка КАК СтатьяДоходов,
	ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион КАК ВГО,
	ВЫБОР
		КОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
			ТОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Контрагент
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ КАК КонтрагентВГО,
	ВыручкаИСебестоимостьПродаж.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельности,
	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ikon_КатегорияПродукта КАК КатегорияРС,
	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.Производитель КАК Бренд,
	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВыручкиБезНДС) КАК Сумма
ПОМЕСТИТЬ ВТ_ВыручкаИСебестоимостьПродажДоходы
ИЗ
	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиДоходов КАК СтатьиДоходов
		ПО (СтатьиДоходов.Код = ВЫБОР
//					ТОГДА "Z111000" //&СтатьяДоходов_ЛокальныеПродажи
				КОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
						И ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
					ТОГДА "Z111000"
				КОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
						И НЕ ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион.Ссылка ЕСТЬ NULL
					ТОГДА "Z121000"//&СтатьяДоходов_ЭкспортКод
				ИНАЧЕ "Z719000"//&СтатьяДоходов_ПрочиеКод
			КОНЕЦ)
ГДЕ
	ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	И ВыручкаИСебестоимостьПродаж.СуммаВыручкиБезНДС <> 0

СГРУППИРОВАТЬ ПО
	НАЧАЛОПЕРИОДА(ВыручкаИСебестоимостьПродаж.Период, МЕСЯЦ),
	ВыручкаИСебестоимостьПродаж.Подразделение,
	ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион,
	ВЫБОР
		КОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
			ТОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Контрагент
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ,
	ВыручкаИСебестоимостьПродаж.НаправлениеДеятельностиКонтрагента,
	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ikon_КатегорияПродукта,
	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.Производитель,
	СтатьиДоходов.Ссылка
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ВыручкаИСебестоимостьПродажОбороты.Период, МЕСЯЦ) КАК Период,
	ВыручкаИСебестоимостьПродажОбороты.Подразделение КАК Подразделение,
	СтатьиРасходов.Ссылка КАК СтатьяРасходов,
	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион КАК ВГО,
	ВЫБОР
		КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
			ТОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Контрагент
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ КАК КонтрагентВГО,
	ВыручкаИСебестоимостьПродажОбороты.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельности,
	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.ikon_КатегорияПродукта КАК КатегорияРС,
	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.Производитель КАК Бренд,
	СУММА(ВыручкаИСебестоимостьПродажОбороты.СтоимостьУпрОборот + ВыручкаИСебестоимостьПродажОбороты.ДопРасходыУпрОборот + ВыручкаИСебестоимостьПродажОбороты.ПостатейныеПостоянныеУпрОборот + ВыручкаИСебестоимостьПродажОбороты.ПостатейныеПеременныеУпрОборот + ВыручкаИСебестоимостьПродажОбороты.РасходыНаПродажуУпрОборот + ВыручкаИСебестоимостьПродажОбороты.ТрудозатратыУпрОборот) КАК Сумма
	//МИНИМУМ(ВЫБОР КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
	//		ТОГДА &Себестоимость_СчетУчета
	//	  ИНАЧЕ ПорядокОтраженияНаСчетахУчета.СчетУчета
	//   КОНЕЦ) КАК СчетУчета,
ПОМЕСТИТЬ ВТ_ВыручкаИСебестоимостьПродажРасходы
ИЗ
	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&ДатаНачала, &ДатаОкончания, Месяц, ) КАК ВыручкаИСебестоимостьПродажОбороты
		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		ПО (СтатьиРасходов.Код = ВЫБОР
				КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
					ТОГДА "Z390000_1"//&СтатьяРасходов_ИзменениеКод
				ИНАЧЕ "Z729000_3"//&СтатьяРасходов_ПрочиеКод
			КОНЕЦ)
		//ПО СтатьиРасходов.Ссылка = 
		//	ВЫБОР КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
		//			ТОГДА &СтатьяРасходов_Изменение
		//		 ИНАЧЕ &СтатьяРасходов_Прочие
		//    КОНЕЦ
			//ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
			//ПО СтатьиРасходов.ГруппаФинансовогоУчета = ПорядокОтраженияНаСчетахУчета.АналитикаУчета
ГДЕ
	ВыручкаИСебестоимостьПродажОбороты.СтоимостьУпрОборот + ВыручкаИСебестоимостьПродажОбороты.ДопРасходыУпрОборот + ВыручкаИСебестоимостьПродажОбороты.ПостатейныеПостоянныеУпрОборот + ВыручкаИСебестоимостьПродажОбороты.ПостатейныеПеременныеУпрОборот + ВыручкаИСебестоимостьПродажОбороты.РасходыНаПродажуУпрОборот + ВыручкаИСебестоимостьПродажОбороты.ТрудозатратыУпрОборот <> 0

СГРУППИРОВАТЬ ПО
	НАЧАЛОПЕРИОДА(ВыручкаИСебестоимостьПродажОбороты.Период, МЕСЯЦ),
	ВыручкаИСебестоимостьПродажОбороты.Подразделение,
	СтатьиРасходов.Ссылка,
	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион,
	ВЫБОР
		КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
			ТОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Контрагент
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ,
	ВыручкаИСебестоимостьПродажОбороты.НаправлениеДеятельностиКонтрагента,
	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.ikon_КатегорияПродукта,
	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.Производитель
;

////////////////////////////////////////////////////////////////////////////////
// ПрочиеРасходы //////////////////////////////////////////////////////////////////////////////////////////////////////// 
ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ) КАК Период,
	ПрочиеРасходы.Подразделение КАК Подразделение,
	ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	ВЫРАЗИТЬ(ПрочиеРасходы.АналитикаУчетаНоменклатуры.Номенклатура.ikon_КатегорияПродукта КАК Справочник.ikon_КатегорииПродуктов) КАК КатегорияРС,
	ВЫРАЗИТЬ(ПрочиеРасходы.АналитикаУчетаНоменклатуры.Номенклатура.Производитель КАК Справочник.Производители) КАК Производитель,
	ПрочиеРасходы.Регистратор КАК Регистратор,
	ПрочиеРасходы.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	СУММА(ПрочиеРасходы.СуммаУпр) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыРасход
ИЗ
	РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
ГДЕ
	ПрочиеРасходы.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	И ПрочиеРасходы.ХозяйственнаяОперация В(&ХозяйственныеОперации)
	И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	И ПрочиеРасходы.СуммаУпр <> 0

СГРУППИРОВАТЬ ПО
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ),
	ПрочиеРасходы.Подразделение,
	ПрочиеРасходы.СтатьяРасходов,
	ВЫРАЗИТЬ(ПрочиеРасходы.АналитикаУчетаНоменклатуры.Номенклатура.ikon_КатегорияПродукта КАК Справочник.ikon_КатегорииПродуктов),
	ВЫРАЗИТЬ(ПрочиеРасходы.АналитикаУчетаНоменклатуры.Номенклатура.Производитель КАК Справочник.Производители),
	ПрочиеРасходы.Регистратор,
	ПрочиеРасходы.ИдентификаторФинЗаписи

ИМЕЮЩИЕ
	СУММА(ПрочиеРасходы.СуммаУпр) <> 0
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПрочиеРасходы.Период КАК Период,
	ПрочиеРасходы.Подразделение КАК Подразделение,
	ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	СУММА(ПрочиеРасходы.Сумма) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыРасходИтоги
ИЗ
	ВТ_ПрочиеРасходыРасход КАК ПрочиеРасходы

СГРУППИРОВАТЬ ПО
	ПрочиеРасходы.Период,
	ПрочиеРасходы.Подразделение,
	ПрочиеРасходы.СтатьяРасходов

ИМЕЮЩИЕ
	СУММА(ПрочиеРасходы.Сумма) <> 0
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ) КАК Период,
	ПрочиеРасходы.Подразделение КАК Подразделение,
	ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	ВЫБОР
		КОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
			ТОГДА ПриобретениеТоваровУслуг.Контрагент
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ КАК КонтрагентВГО,
	СУММА(ПрочиеРасходы.СуммаУпр) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыПриходПредварительно
ИЗ
	РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходыРасходИтоги КАК ПрочиеРасходыРасходИтоги
		ПО (НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПрочиеРасходыРасходИтоги.Период, МЕСЯЦ))
			И ПрочиеРасходы.Подразделение = ПрочиеРасходыРасходИтоги.Подразделение
			И ПрочиеРасходы.СтатьяРасходов = ПрочиеРасходыРасходИтоги.СтатьяРасходов
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
		ПО ПрочиеРасходы.Регистратор = ПриобретениеТоваровУслуг.Ссылка
ГДЕ
	ПрочиеРасходы.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	И ПрочиеРасходы.СуммаУпр <> 0

СГРУППИРОВАТЬ ПО
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ),
	ПрочиеРасходы.Подразделение,
	ПрочиеРасходы.СтатьяРасходов,
	ВЫБОР
		КОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
			ТОГДА ПриобретениеТоваровУслуг.Контрагент
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ),
	ПрочиеРасходы.Подразделение,
	ПрочиеРасходы.СтатьяРасходов,
	ВЫБОР
		КОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
			ТОГДА ПриобретениеТоваровУслуг.Контрагент
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ,
	СУММА(ПрочиеРасходы.СуммаУпр)
ИЗ
	РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходыРасходИтоги КАК ПрочиеРасходыРасходИтоги
		ПО (НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПрочиеРасходыРасходИтоги.Период, МЕСЯЦ))
			И ПрочиеРасходы.Подразделение = ПрочиеРасходыРасходИтоги.Подразделение
			И ПрочиеРасходы.СтатьяРасходов = ПрочиеРасходыРасходИтоги.СтатьяРасходов
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Сторно КАК ДокументСторно
		ПО ПрочиеРасходы.Регистратор = ДокументСторно.Ссылка
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
		ПО (ДокументСторно.СторнируемыйДокумент = ПриобретениеТоваровУслуг.Ссылка)
ГДЕ
	ПрочиеРасходы.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	И ПрочиеРасходы.СуммаУпр <> 0

СГРУППИРОВАТЬ ПО
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ),
	ПрочиеРасходы.Подразделение,
	ПрочиеРасходы.СтатьяРасходов,
	ВЫБОР
		КОГДА ПриобретениеТоваровУслуг.Контрагент.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
			ТОГДА ПриобретениеТоваровУслуг.Контрагент
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПрочиеРасходы.Период КАК Период,
	ПрочиеРасходы.Подразделение КАК Подразделение,
	ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	ПрочиеРасходы.КонтрагентВГО КАК КонтрагентВГО,
	СУММА(ПрочиеРасходы.Сумма) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыПриход
ИЗ
	ВТ_ПрочиеРасходыПриходПредварительно КАК ПрочиеРасходы

СГРУППИРОВАТЬ ПО
	ПрочиеРасходы.Период,
	ПрочиеРасходы.Подразделение,
	ПрочиеРасходы.СтатьяРасходов,
	ПрочиеРасходы.КонтрагентВГО

ИМЕЮЩИЕ
	СУММА(ПрочиеРасходы.Сумма) <> 0
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПрочиеРасходы.Период КАК Период,
	ПрочиеРасходы.Подразделение КАК Подразделение,
	ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	СУММА(ПрочиеРасходы.Сумма) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыПриходИтоги
ИЗ
	ВТ_ПрочиеРасходыПриходПредварительно КАК ПрочиеРасходы

СГРУППИРОВАТЬ ПО
	ПрочиеРасходы.Период,
	ПрочиеРасходы.Подразделение,
	ПрочиеРасходы.СтатьяРасходов

ИМЕЮЩИЕ
	СУММА(ПрочиеРасходы.Сумма) <> 0
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПрочиеРасходыРасход.Период КАК Период,
	ПрочиеРасходыРасход.Подразделение КАК Подразделение,
	ПрочиеРасходыРасход.СтатьяРасходов КАК СтатьяРасходов,
	ПрочиеРасходыРасход.КатегорияРС КАК КатегорияРС,
	ПрочиеРасходыРасход.Производитель КАК Производитель,
	ПрочиеРасходыРасход.Регистратор КАК Регистратор,
	ПрочиеРасходыРасход.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	ПрочиеРасходыПриход.КонтрагентВГО КАК КонтрагентВГО,
	ВЫРАЗИТЬ(ПрочиеРасходыРасход.Сумма * ПрочиеРасходыПриход.Сумма / ПрочиеРасходыПриходИтоги.Сумма КАК ЧИСЛО(20, 2)) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыРасходРаспределено
ИЗ
	ВТ_ПрочиеРасходыРасход КАК ПрочиеРасходыРасход
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходыПриход КАК ПрочиеРасходыПриход
		ПО ПрочиеРасходыРасход.Период = ПрочиеРасходыПриход.Период
			И ПрочиеРасходыРасход.Подразделение = ПрочиеРасходыПриход.Подразделение
			И ПрочиеРасходыРасход.СтатьяРасходов = ПрочиеРасходыПриход.СтатьяРасходов
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходыПриходИтоги КАК ПрочиеРасходыПриходИтоги
		ПО (ПрочиеРасходыПриход.Период = ПрочиеРасходыПриходИтоги.Период)
			И (ПрочиеРасходыПриход.Подразделение = ПрочиеРасходыПриходИтоги.Подразделение)
			И (ПрочиеРасходыПриход.СтатьяРасходов = ПрочиеРасходыПриходИтоги.СтатьяРасходов)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПрочиеРасходыРасходРаспределено.Период КАК Период,
	ПрочиеРасходыРасходРаспределено.Подразделение КАК Подразделение,
	ПрочиеРасходыРасходРаспределено.СтатьяРасходов КАК СтатьяРасходов,
	ПрочиеРасходыРасходРаспределено.КатегорияРС КАК КатегорияРС,
	ПрочиеРасходыРасходРаспределено.Производитель КАК Производитель,
	ПрочиеРасходыРасходРаспределено.Регистратор КАК Регистратор,
	ПрочиеРасходыРасходРаспределено.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	СУММА(ПрочиеРасходыРасходРаспределено.Сумма) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыРасходРаспределеноИтоги
//			ТОГДА ЗНАЧЕНИЕ(Справочник.БизнесРегионы.ПустаяСсылка) //&БизнесРегион_Внешний
ИЗ
	ВТ_ПрочиеРасходыРасходРаспределено КАК ПрочиеРасходыРасходРаспределено

СГРУППИРОВАТЬ ПО
	ПрочиеРасходыРасходРаспределено.Период,
	ПрочиеРасходыРасходРаспределено.Подразделение,
	ПрочиеРасходыРасходРаспределено.СтатьяРасходов,
	ПрочиеРасходыРасходРаспределено.КатегорияРС,
	ПрочиеРасходыРасходРаспределено.Производитель,
	ПрочиеРасходыРасходРаспределено.Регистратор,
	ПрочиеРасходыРасходРаспределено.ИдентификаторФинЗаписи
//			ТОГДА ЗНАЧЕНИЕ(Справочник.БизнесРегионы.ПустаяСсылка) //&БизнесРегион_Внешний
;

////////////////////////////////////////////////////////////////////////////////
// Линии PL ///////////////////////////////////////////////////////////////////////////////////////////////////////////// 
ВЫБРАТЬ
	ПрочиеРасходыРасходРаспределено.Период КАК Период,
	ПрочиеРасходыРасходРаспределено.Подразделение КАК Подразделение,
	ПрочиеРасходыРасходРаспределено.СтатьяРасходов КАК СтатьяРасходов,
	ПрочиеРасходыРасходРаспределено.КатегорияРС КАК КатегорияРС,
	ПрочиеРасходыРасходРаспределено.Производитель КАК Производитель,
	ПрочиеРасходыРасходРаспределено.Регистратор КАК Регистратор,
	ПрочиеРасходыРасходРаспределено.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	ПрочиеРасходыРасходРаспределено.КонтрагентВГО КАК КонтрагентВГО,
	ПрочиеРасходыРасходРаспределено.Сумма КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходыРасходОткорректировано
ИЗ
	ВТ_ПрочиеРасходыРасходРаспределено КАК ПрочиеРасходыРасходРаспределено

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ПрочиеРасходыРасход.Период,
	ПрочиеРасходыРасход.Подразделение,
	ПрочиеРасходыРасход.СтатьяРасходов,
	ПрочиеРасходыРасход.КатегорияРС,
	ПрочиеРасходыРасход.Производитель,
	ПрочиеРасходыРасход.Регистратор,
	ПрочиеРасходыРасход.ИдентификаторФинЗаписи,
	НЕОПРЕДЕЛЕНО,
	ПрочиеРасходыРасход.Сумма - ЕСТЬNULL(ПрочиеРасходыРасходРаспределеноИтоги.Сумма, 0)
ИЗ
	ВТ_ПрочиеРасходыРасход КАК ПрочиеРасходыРасход
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходыРасходРаспределеноИтоги КАК ПрочиеРасходыРасходРаспределеноИтоги
		ПО ПрочиеРасходыРасход.Период = ПрочиеРасходыРасходРаспределеноИтоги.Период
			И ПрочиеРасходыРасход.Подразделение = ПрочиеРасходыРасходРаспределеноИтоги.Подразделение
			И ПрочиеРасходыРасход.СтатьяРасходов = ПрочиеРасходыРасходРаспределеноИтоги.СтатьяРасходов
			И (ВЫБОР
				КОГДА ПрочиеРасходыРасход.Производитель.Ссылка ЕСТЬ NULL
						И ПрочиеРасходыРасходРаспределеноИтоги.Производитель.Ссылка ЕСТЬ NULL
					ТОГДА ИСТИНА
				ИНАЧЕ ПрочиеРасходыРасход.Производитель = ПрочиеРасходыРасходРаспределеноИтоги.Производитель
			КОНЕЦ)
			И ПрочиеРасходыРасход.Регистратор = ПрочиеРасходыРасходРаспределеноИтоги.Регистратор
			И ПрочиеРасходыРасход.ИдентификаторФинЗаписи = ПрочиеРасходыРасходРаспределеноИтоги.ИдентификаторФинЗаписи
ГДЕ
	ПрочиеРасходыРасход.Сумма - ЕСТЬNULL(ПрочиеРасходыРасходРаспределеноИтоги.Сумма, 0) <> 0
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПрочиеРасходыРасходОткорректировано.Период КАК Период,
	ПрочиеРасходыРасходОткорректировано.Подразделение КАК Подразделение,
	ПрочиеРасходыРасходОткорректировано.СтатьяРасходов КАК СтатьяРасходов,
	ПрочиеРасходыРасходОткорректировано.КатегорияРС КАК КатегорияРС,
	ПрочиеРасходыРасходОткорректировано.Производитель КАК Производитель,
	ПрочиеРасходыРасходОткорректировано.Регистратор КАК Регистратор,
	ПрочиеРасходыРасходОткорректировано.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	ВЫБОР
		КОГДА ВЫРАЗИТЬ(ПрочиеРасходыРасходОткорректировано.КонтрагентВГО.Партнер КАК Справочник.Партнеры).Ссылка ЕСТЬ NULL
			ТОГДА ЗНАЧЕНИЕ(Справочник.БизнесРегионы.ПустаяСсылка)
		ИНАЧЕ ВЫРАЗИТЬ(ПрочиеРасходыРасходОткорректировано.КонтрагентВГО.Партнер КАК Справочник.Партнеры).БизнесРегион
	КОНЕЦ КАК ВГО,
	ПрочиеРасходыРасходОткорректировано.КонтрагентВГО КАК КонтрагентВГО,
	СУММА(ПрочиеРасходыРасходОткорректировано.Сумма) КАК Сумма
ПОМЕСТИТЬ ВТ_ПрочиеРасходы
ИЗ
	ВТ_ПрочиеРасходыРасходОткорректировано КАК ПрочиеРасходыРасходОткорректировано

СГРУППИРОВАТЬ ПО
	ПрочиеРасходыРасходОткорректировано.Период,
	ПрочиеРасходыРасходОткорректировано.Подразделение,
	ПрочиеРасходыРасходОткорректировано.СтатьяРасходов,
	ПрочиеРасходыРасходОткорректировано.КатегорияРС,
	ПрочиеРасходыРасходОткорректировано.Производитель,
	ПрочиеРасходыРасходОткорректировано.Регистратор,
	ПрочиеРасходыРасходОткорректировано.ИдентификаторФинЗаписи,
	ВЫБОР
		КОГДА ВЫРАЗИТЬ(ПрочиеРасходыРасходОткорректировано.КонтрагентВГО.Партнер КАК Справочник.Партнеры).Ссылка ЕСТЬ NULL
			ТОГДА ЗНАЧЕНИЕ(Справочник.БизнесРегионы.ПустаяСсылка)
		ИНАЧЕ ВЫРАЗИТЬ(ПрочиеРасходыРасходОткорректировано.КонтрагентВГО.Партнер КАК Справочник.Партнеры).БизнесРегион
	КОНЕЦ,
	ПрочиеРасходыРасходОткорректировано.КонтрагентВГО

ИМЕЮЩИЕ
	СУММА(ПрочиеРасходыРасходОткорректировано.Сумма) <> 0
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ РАЗЛИЧНЫЕ
	ВыручкаИСебестоимостьПродаж.Подразделение КАК Подразделение,
	ВыручкаИСебестоимостьПродаж.СтатьяРасходов КАК СтатьяРасходов
ПОМЕСТИТЬ ВТ_СтатьиРасходовПодразделения
ИЗ
	ВТ_ВыручкаИСебестоимостьПродажРасходы КАК ВыручкаИСебестоимостьПродаж

ОБЪЕДИНИТЬ

// ПрочиеДоходы И ПрочиеРасходы ///////////////////////////////////////////////////////////////////////////////////////// 
ВЫБРАТЬ
	ПрочиеРасходы.Подразделение,
	ПрочиеРасходы.СтатьяРасходов
ИЗ
	ВТ_ПрочиеРасходы КАК ПрочиеРасходы
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ РАЗЛИЧНЫЕ
	1 КАК Приоритет,
	СтатьиРасходовПодразделения.Подразделение КАК Подразделение,
	СтатьиРасходовПодразделения.СтатьяРасходов КАК СтатьяРасходов,
	СтатьиРасходовПодразделения.СтатьяРасходов КАК СтатьяРасходовОтбор
ПОМЕСТИТЬ ВТ_СтатьиРасходовПодразделенияПриоритеты
ИЗ
	ВТ_СтатьиРасходовПодразделения КАК СтатьиРасходовПодразделения

ОБЪЕДИНИТЬ

ВЫБРАТЬ
	2,
	СтатьиРасходовПодразделения.Подразделение,
	СтатьиРасходовПодразделения.СтатьяРасходов,
	СтатьиРасходовПодразделения.СтатьяРасходов.Родитель
ИЗ
	ВТ_СтатьиРасходовПодразделения КАК СтатьиРасходовПодразделения

ОБЪЕДИНИТЬ

// Результат //////////////////////////////////////////////////////////////////////////////////////////////////////////// 
ВЫБРАТЬ
	3,
	СтатьиРасходовПодразделения.Подразделение,
	СтатьиРасходовПодразделения.СтатьяРасходов,
	СтатьиРасходовПодразделения.СтатьяРасходов.Родитель.Родитель
	//МИНИМУМ(ПорядокОтраженияНаСчетахУчета.СчетУчета) КАК СчетУчета,
ИЗ
	ВТ_СтатьиРасходовПодразделения КАК СтатьиРасходовПодразделения
;

////////////////////////////////////////////////////////////////////////////////
			//ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
			//ПО СтатьиДоходов.ГруппаФинансовогоУчета = ПорядокОтраженияНаСчетахУчета.АналитикаУчета
ВЫБРАТЬ
	СтатьиРасходовПодразделенияПриоритеты.Подразделение КАК Подразделение,
	СтатьиРасходовПодразделенияПриоритеты.СтатьяРасходов КАК СтатьяРасходов,
	СтатьиРасходовПодразделенияПриоритеты.Приоритет КАК Приоритет,
	ikon_ЛинииPLСтатейРасходов.ЛинияPL КАК ЛинияPL
ПОМЕСТИТЬ ВТ_ЛинииPLСтатейРасходовПриоритеты
	//ВыручкаИСебестоимостьПродаж.СчетУчета КАК СчетУчета,
ИЗ
	ВТ_СтатьиРасходовПодразделенияПриоритеты КАК СтатьиРасходовПодразделенияПриоритеты
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ikon_ЛинииPLСтатейРасходов КАК ikon_ЛинииPLСтатейРасходов
		ПО СтатьиРасходовПодразделенияПриоритеты.Подразделение = ikon_ЛинииPLСтатейРасходов.Подразделение
			И СтатьиРасходовПодразделенияПриоритеты.СтатьяРасходовОтбор = ikon_ЛинииPLСтатейРасходов.СтатьяРасходов
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЛинииPLСтатейРасходовПриоритеты.Подразделение КАК Подразделение,
	ЛинииPLСтатейРасходовПриоритеты.СтатьяРасходов КАК СтатьяРасходов,
	МИНИМУМ(ЛинииPLСтатейРасходовПриоритеты.Приоритет) КАК Приоритет
ПОМЕСТИТЬ ВТ_ЛинииPLСтатейРасходовМинимальныеПриоритеты
	//МИНИМУМ(ПорядокОтраженияНаСчетахУчета.СчетУчета) КАК СчетУчета,
ИЗ
	ВТ_ЛинииPLСтатейРасходовПриоритеты КАК ЛинииPLСтатейРасходовПриоритеты

			//ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
			//ПО ПрочиеДоходыРасходы.ГруппаФинансовогоУчета = ПорядокОтраженияНаСчетахУчета.АналитикаУчета
СГРУППИРОВАТЬ ПО
	ЛинииPLСтатейРасходовПриоритеты.Подразделение,
	ЛинииPLСтатейРасходовПриоритеты.СтатьяРасходов
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЛинииPLСтатейРасходовПриоритеты.Подразделение КАК Подразделение,
	ЛинииPLСтатейРасходовПриоритеты.СтатьяРасходов КАК СтатьяРасходов,
	МАКСИМУМ(ЛинииPLСтатейРасходовПриоритеты.ЛинияPL) КАК ЛинияPL
ПОМЕСТИТЬ ВТ_ЛинииPLСтатейРасходов
	//МИНИМУМ(ПорядокОтраженияНаСчетахУчета.СчетУчета) КАК СчетУчета,
ИЗ
	ВТ_ЛинииPLСтатейРасходовПриоритеты КАК ЛинииPLСтатейРасходовПриоритеты
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЛинииPLСтатейРасходовМинимальныеПриоритеты КАК ЛинииPLСтатейРасходовМинимальныеПриоритеты
		ПО ЛинииPLСтатейРасходовПриоритеты.Подразделение = ЛинииPLСтатейРасходовМинимальныеПриоритеты.Подразделение
			И ЛинииPLСтатейРасходовПриоритеты.СтатьяРасходов = ЛинииPLСтатейРасходовМинимальныеПриоритеты.СтатьяРасходов
			И ЛинииPLСтатейРасходовПриоритеты.Приоритет = ЛинииPLСтатейРасходовМинимальныеПриоритеты.Приоритет

		//ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
		//ПО ПрочаяВыручкаОбороты.СтатьяДоходов.ГруппаФинансовогоУчета = ПорядокОтраженияНаСчетахУчета.АналитикаУчета
СГРУППИРОВАТЬ ПО
	ЛинииPLСтатейРасходовПриоритеты.Подразделение,
	ЛинииPLСтатейРасходовПриоритеты.СтатьяРасходов
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ПрочиеДоходы.Период, МЕСЯЦ) КАК Период,
	ПрочиеДоходы.Подразделение КАК Подразделение,
	ПрочиеДоходы.Подразделение.Код КАК ПодразделениеКод,
	ПрочиеДоходы.СтатьяДоходов КАК СтатьяРасходов,
	ПрочиеДоходы.СтатьяДоходов.Код КАК СтатьяРасходовКод,
	ПрочиеДоходы.СтатьяДоходов КАК СтатьяЗатрат,
	ПрочиеДоходы.СтатьяДоходов.Код КАК СтатьяЗатратКод,
	ПрочиеДоходы.СтатьяДоходов.Родитель КАК СтатьяБюджета,
	ПрочиеДоходы.СтатьяДоходов.Родитель.Код КАК СтатьяБюджетаКод,
	ПрочиеДоходы.СтатьяДоходов.ikon_ЛинияPL КАК ЛинияPL,
	ПрочиеДоходы.СтатьяДоходов.ikon_ЛинияPL.Код КАК ЛинияPLКод,
	НЕОПРЕДЕЛЕНО КАК ВГО,
	НЕОПРЕДЕЛЕНО КАК КонтрагентВГО,
	ВЫРАЗИТЬ(ПрочиеДоходы.АналитикаДоходов КАК Справочник.Номенклатура).ikon_КатегорияПродукта КАК КатегорияРС,
	ВЫРАЗИТЬ(ПрочиеДоходы.АналитикаДоходов КАК Справочник.Номенклатура).Производитель КАК Бренд,
	ПрочиеДоходы.Регистратор КАК Регистратор,
	ПрочиеДоходы.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	ПрочиеДоходы.СтатьяДоходов.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	ПрочиеДоходы.СуммаУпр КАК Сумма,
	ПрочиеДоходы.СуммаУпр КАК СуммаФР
ПОМЕСТИТЬ ВТ_ПрочиеДоходыРасходы
ИЗ
	РегистрНакопления.ПрочиеДоходы КАК ПрочиеДоходы
ГДЕ
	ПрочиеДоходы.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	И ПрочиеДоходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	И ПрочиеДоходы.СуммаУпр <> 0

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ПрочиеРасходы.Период, МЕСЯЦ),
	ПрочиеРасходы.Подразделение,
	ПрочиеРасходы.Подразделение.Код,
	ПрочиеРасходы.СтатьяРасходов,
	ПрочиеРасходы.СтатьяРасходов.Код,
	ПрочиеРасходы.СтатьяРасходов.Родитель,
	ПрочиеРасходы.СтатьяРасходов.Родитель.Код,
	ПрочиеРасходы.СтатьяРасходов.Родитель.Родитель,
	ПрочиеРасходы.СтатьяРасходов.Родитель.Родитель.Код,
	ЛинииPLСтатейРасходов.ЛинияPL,
	ЛинииPLСтатейРасходов.ЛинияPL.Код,
	ПрочиеРасходы.ВГО,
	ПрочиеРасходы.КонтрагентВГО,
	ПрочиеРасходы.КатегорияРС,
	ПрочиеРасходы.Производитель,
	ПрочиеРасходы.Регистратор,
	ПрочиеРасходы.ИдентификаторФинЗаписи,
	ЕСТЬNULL(ikon_ПравилаПерекрестнойПроверки.ГФУ, ПрочиеРасходы.СтатьяРасходов.ГруппаФинансовогоУчета),
	ПрочиеРасходы.Сумма,
	-ПрочиеРасходы.Сумма
ИЗ
	ВТ_ПрочиеРасходы КАК ПрочиеРасходы
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЛинииPLСтатейРасходов КАК ЛинииPLСтатейРасходов
		ПО ПрочиеРасходы.Подразделение = ЛинииPLСтатейРасходов.Подразделение
			И ПрочиеРасходы.СтатьяРасходов = ЛинииPLСтатейРасходов.СтатьяРасходов
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ikon_ПравилаПерекрестнойПроверки КАК ikon_ПравилаПерекрестнойПроверки
		ПО ПрочиеРасходы.Подразделение = ikon_ПравилаПерекрестнойПроверки.Подразделения
			И ПрочиеРасходы.СтатьяРасходов = ikon_ПравилаПерекрестнойПроверки.СтатьяРасходов
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ РАЗЛИЧНЫЕ
	ПрочиеДоходыРасходы.Регистратор КАК Регистратор,
	ПрочиеДоходыРасходы.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи
ПОМЕСТИТЬ ВТ_РегистраторыИдентификаторы
ИЗ
	ВТ_ПрочиеДоходыРасходы КАК ПрочиеДоходыРасходы
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ДвиженияПоПрочимАктивамПассивам.Регистратор КАК Регистратор,
	ДвиженияПоПрочимАктивамПассивам.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	МАКСИМУМ(ДвиженияПоПрочимАктивамПассивам.НаправлениеДеятельности) КАК НаправлениеДеятельности
ПОМЕСТИТЬ ВТ_ДвиженияПоПрочимАктивамПассивам
ИЗ
	ВТ_РегистраторыИдентификаторы КАК РегистраторыИдентификаторы
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК ДвиженияПоПрочимАктивамПассивам
		ПО РегистраторыИдентификаторы.Регистратор = ДвиженияПоПрочимАктивамПассивам.Регистратор
			И РегистраторыИдентификаторы.ИдентификаторФинЗаписи = ДвиженияПоПрочимАктивамПассивам.ИдентификаторФинЗаписи

СГРУППИРОВАТЬ ПО
	ДвиженияПоПрочимАктивамПассивам.Регистратор,
	ДвиженияПоПрочимАктивамПассивам.ИдентификаторФинЗаписи
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	"Выручка и себестоимость продаж (Доходы)" КАК РегистрИсточник,
	ВыручкаИСебестоимостьПродаж.Период КАК Период,
	ВыручкаИСебестоимостьПродаж.Подразделение КАК Подразделение,
	ВыручкаИСебестоимостьПродаж.Подразделение.Код КАК ПодразделениеКод,
	СтатьиДоходов.Ссылка КАК СтатьяРасходов,
	СтатьиДоходов.Код КАК СтатьяРасходовКод,
	СтатьиДоходов.Ссылка КАК СтатьяЗатрат,
	СтатьиДоходов.Код КАК СтатьяЗатратКод,
	СтатьиДоходов.Родитель КАК СтатьяБюджета,
	СтатьиДоходов.Родитель.Код КАК СтатьяБюджетаКод,
	ВЫБОР
		КОГДА ВыручкаИСебестоимостьПродаж.ВГО.Наименование ПОДОБНО "ВГО %"
				И НЕ СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО
		ИНАЧЕ СтатьиДоходов.ikon_ЛинияPL
	КОНЕЦ КАК ЛинияPL,
	ВЫБОР
		КОГДА ВыручкаИСебестоимостьПродаж.ВГО.Наименование ПОДОБНО "ВГО %"
				И НЕ СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Код
		ИНАЧЕ СтатьиДоходов.ikon_ЛинияPL.Код
	КОНЕЦ КАК ЛинияPLКод,
	ВыручкаИСебестоимостьПродаж.ВГО КАК ВГО,
	ВыручкаИСебестоимостьПродаж.КонтрагентВГО КАК КонтрагентВГО,
	ВыручкаИСебестоимостьПродаж.НаправлениеДеятельности КАК НаправлениеДеятельности,
	ВыручкаИСебестоимостьПродаж.КатегорияРС КАК КатегорияРС,
	ВыручкаИСебестоимостьПродаж.Бренд КАК Бренд,
	СУММА(ВыручкаИСебестоимостьПродаж.Сумма) КАК Сумма,
	СУММА(ВыручкаИСебестоимостьПродаж.Сумма) КАК СуммаФР
ПОМЕСТИТЬ ВТ_РезультатПредварительно
ИЗ
	ВТ_ВыручкаИСебестоимостьПродажДоходы КАК ВыручкаИСебестоимостьПродаж
		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиДоходов КАК СтатьиДоходов
		ПО ВыручкаИСебестоимостьПродаж.СтатьяДоходов = СтатьиДоходов.Ссылка

СГРУППИРОВАТЬ ПО
	ВыручкаИСебестоимостьПродаж.Период,
	ВыручкаИСебестоимостьПродаж.Подразделение,
	СтатьиДоходов.Ссылка,
	ВЫБОР
		КОГДА ВыручкаИСебестоимостьПродаж.ВГО.Наименование ПОДОБНО "ВГО %"
				И НЕ СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО
		ИНАЧЕ СтатьиДоходов.ikon_ЛинияPL
	КОНЕЦ,
	ВЫБОР
		КОГДА ВыручкаИСебестоимостьПродаж.ВГО.Наименование ПОДОБНО "ВГО %"
				И НЕ СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА СтатьиДоходов.ikon_ЛинияPL.ЛинияВГО.Код
		ИНАЧЕ СтатьиДоходов.ikon_ЛинияPL.Код
	КОНЕЦ,
	ВыручкаИСебестоимостьПродаж.ВГО,
	ВыручкаИСебестоимостьПродаж.КонтрагентВГО,
	ВыручкаИСебестоимостьПродаж.НаправлениеДеятельности,
	ВыручкаИСебестоимостьПродаж.КатегорияРС,
	ВыручкаИСебестоимостьПродаж.Бренд,
	ВыручкаИСебестоимостьПродаж.Подразделение.Код,
	СтатьиДоходов.Код,
	СтатьиДоходов.Ссылка,
	СтатьиДоходов.Код,
	СтатьиДоходов.Родитель,
	СтатьиДоходов.Родитель.Код

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	"Выручка и себестоимость продаж (Расходы)",
	ВыручкаИСебестоимостьПродаж.Период,
	ВыручкаИСебестоимостьПродаж.Подразделение,
	ВыручкаИСебестоимостьПродаж.Подразделение.Код,
	ВыручкаИСебестоимостьПродаж.СтатьяРасходов,
	ВыручкаИСебестоимостьПродаж.СтатьяРасходов.Код,
	ВыручкаИСебестоимостьПродаж.СтатьяРасходов.Родитель,
	ВыручкаИСебестоимостьПродаж.СтатьяРасходов.Родитель.Код,
	ВыручкаИСебестоимостьПродаж.СтатьяРасходов.Родитель.Родитель,
	ВыручкаИСебестоимостьПродаж.СтатьяРасходов.Родитель.Родитель.Код,
	ВЫБОР
		КОГДА ВыручкаИСебестоимостьПродаж.ВГО.Наименование ПОДОБНО "ВГО %"
				И НЕ ЛинииPLСтатейРасходов.ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА ЛинииPLСтатейРасходов.ЛинияPL.ЛинияВГО
		ИНАЧЕ ЛинииPLСтатейРасходов.ЛинияPL
	КОНЕЦ,
	ВЫБОР
		КОГДА ВыручкаИСебестоимостьПродаж.ВГО.Наименование ПОДОБНО "ВГО %"
				И НЕ ЛинииPLСтатейРасходов.ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА ЛинииPLСтатейРасходов.ЛинияPL.ЛинияВГО.Код
		ИНАЧЕ ЛинииPLСтатейРасходов.ЛинияPL.Код
	КОНЕЦ,
	ВыручкаИСебестоимостьПродаж.ВГО,
	ВыручкаИСебестоимостьПродаж.КонтрагентВГО,
	ВыручкаИСебестоимостьПродаж.НаправлениеДеятельности,
	ВыручкаИСебестоимостьПродаж.КатегорияРС,
	ВыручкаИСебестоимостьПродаж.Бренд,
	ВыручкаИСебестоимостьПродаж.Сумма,
	-ВыручкаИСебестоимостьПродаж.Сумма
ИЗ
	ВТ_ВыручкаИСебестоимостьПродажРасходы КАК ВыручкаИСебестоимостьПродаж
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЛинииPLСтатейРасходов КАК ЛинииPLСтатейРасходов
		ПО ВыручкаИСебестоимостьПродаж.Подразделение = ЛинииPLСтатейРасходов.Подразделение
			И ВыручкаИСебестоимостьПродаж.СтатьяРасходов = ЛинииPLСтатейРасходов.СтатьяРасходов

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	"Прочие доходы / Прочие расходы",
	ПрочиеДоходыРасходы.Период,
	ПрочиеДоходыРасходы.Подразделение,
	ПрочиеДоходыРасходы.ПодразделениеКод,
	ПрочиеДоходыРасходы.СтатьяРасходов,
	ПрочиеДоходыРасходы.СтатьяРасходовКод,
	ПрочиеДоходыРасходы.СтатьяЗатрат,
	ПрочиеДоходыРасходы.СтатьяЗатратКод,
	ПрочиеДоходыРасходы.СтатьяБюджета,
	ПрочиеДоходыРасходы.СтатьяБюджетаКод,
	ПрочиеДоходыРасходы.ЛинияPL,
	ПрочиеДоходыРасходы.ЛинияPLКод,
	ПрочиеДоходыРасходы.ВГО,
	ПрочиеДоходыРасходы.КонтрагентВГО,
	ДвиженияПоПрочимАктивамПассивам.НаправлениеДеятельности,
	ПрочиеДоходыРасходы.КатегорияРС,
	ПрочиеДоходыРасходы.Бренд,
	СУММА(ПрочиеДоходыРасходы.Сумма),
	СУММА(ПрочиеДоходыРасходы.СуммаФР)
ИЗ
	ВТ_ПрочиеДоходыРасходы КАК ПрочиеДоходыРасходы
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДвиженияПоПрочимАктивамПассивам КАК ДвиженияПоПрочимАктивамПассивам
		ПО ПрочиеДоходыРасходы.Регистратор = ДвиженияПоПрочимАктивамПассивам.Регистратор
			И ПрочиеДоходыРасходы.ИдентификаторФинЗаписи = ДвиженияПоПрочимАктивамПассивам.ИдентификаторФинЗаписи

СГРУППИРОВАТЬ ПО
	ПрочиеДоходыРасходы.Период,
	ПрочиеДоходыРасходы.Подразделение,
	ПрочиеДоходыРасходы.ПодразделениеКод,
	ПрочиеДоходыРасходы.СтатьяРасходов,
	ПрочиеДоходыРасходы.СтатьяРасходовКод,
	ПрочиеДоходыРасходы.СтатьяЗатрат,
	ПрочиеДоходыРасходы.СтатьяЗатратКод,
	ПрочиеДоходыРасходы.СтатьяБюджета,
	ПрочиеДоходыРасходы.СтатьяБюджетаКод,
	ПрочиеДоходыРасходы.ЛинияPL,
	ПрочиеДоходыРасходы.ЛинияPLКод,
	ПрочиеДоходыРасходы.ВГО,
	ПрочиеДоходыРасходы.КонтрагентВГО,
	ДвиженияПоПрочимАктивамПассивам.НаправлениеДеятельности,
	ПрочиеДоходыРасходы.КатегорияРС,
	ПрочиеДоходыРасходы.Бренд

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	"Прочая выручка",
	ПрочаяВыручкаОбороты.Период,
	ПрочаяВыручкаОбороты.Подразделение,
	ПрочаяВыручкаОбороты.Подразделение.Код,
	ПрочаяВыручкаОбороты.СтатьяДоходов,
	ПрочаяВыручкаОбороты.СтатьяДоходов.Код,
	ПрочаяВыручкаОбороты.СтатьяДоходов,
	ПрочаяВыручкаОбороты.СтатьяДоходов.Код,
	ПрочаяВыручкаОбороты.СтатьяДоходов.Родитель,
	ПрочаяВыручкаОбороты.СтатьяДоходов.Родитель.Код,
	ВЫБОР
		КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
				И НЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО
		ИНАЧЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL
	КОНЕЦ,
	ВЫБОР
		КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
				И НЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Код
		ИНАЧЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.Код
	КОНЕЦ,
	ПрочаяВыручкаОбороты.Партнер.БизнесРегион,
	ВЫБОР
		КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
			ТОГДА ПрочаяВыручкаОбороты.Контрагент
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ,
	ПрочаяВыручкаОбороты.НаправлениеДеятельности,
	ВЫРАЗИТЬ(ПрочаяВыручкаОбороты.АналитикаДоходов КАК Справочник.Номенклатура).ikon_КатегорияПродукта,
	ВЫРАЗИТЬ(ПрочаяВыручкаОбороты.АналитикаДоходов КАК Справочник.Номенклатура).Производитель,
	СУММА(ПрочаяВыручкаОбороты.ВыручкаБезНДСУпрОборот),
	СУММА(ПрочаяВыручкаОбороты.ВыручкаБезНДСУпрОборот)
ИЗ
	РегистрНакопления.ПрочаяВыручка.Обороты(&ДатаНачала, &ДатаОкончания, Месяц, ) КАК ПрочаяВыручкаОбороты
ГДЕ
	ПрочаяВыручкаОбороты.ВыручкаБезНДСУпрОборот <> 0

СГРУППИРОВАТЬ ПО
	ПрочаяВыручкаОбороты.Период,
	ПрочаяВыручкаОбороты.Подразделение,
	ПрочаяВыручкаОбороты.СтатьяДоходов,
	ВЫБОР
		КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
				И НЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО
		ИНАЧЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL
	КОНЕЦ,
	ВЫБОР
		КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
				И НЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Ссылка ЕСТЬ NULL
			ТОГДА ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.ЛинияВГО.Код
		ИНАЧЕ ПрочаяВыручкаОбороты.СтатьяДоходов.ikon_ЛинияPL.Код
	КОНЕЦ,
	ПрочаяВыручкаОбороты.Партнер.БизнесРегион,
	ВЫБОР
		КОГДА ПрочаяВыручкаОбороты.Партнер.БизнесРегион.Наименование ПОДОБНО "ВГО %"
			ТОГДА ПрочаяВыручкаОбороты.Контрагент
		ИНАЧЕ НЕОПРЕДЕЛЕНО
	КОНЕЦ,
	ПрочаяВыручкаОбороты.НаправлениеДеятельности,
	ВЫРАЗИТЬ(ПрочаяВыручкаОбороты.АналитикаДоходов КАК Справочник.Номенклатура).ikon_КатегорияПродукта,
	ВЫРАЗИТЬ(ПрочаяВыручкаОбороты.АналитикаДоходов КАК Справочник.Номенклатура).Производитель,
	ПрочаяВыручкаОбороты.Подразделение.Код,
	ПрочаяВыручкаОбороты.СтатьяДоходов.Код,
	ПрочаяВыручкаОбороты.СтатьяДоходов,
	ПрочаяВыручкаОбороты.СтатьяДоходов.Код,
	ПрочаяВыручкаОбороты.СтатьяДоходов.Родитель,
	ПрочаяВыручкаОбороты.СтатьяДоходов.Родитель.Код
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Результат.Период КАК Период,
	Результат.КатегорияРС КАК КатегорияРС,
	Результат.Бренд КАК Бренд,
	Результат.КонтрагентВГО КАК КонтрагентВГО,
	Результат.ЛинияPL КАК ЛинияPL,
	СУММА(Результат.Сумма) КАК Сумма
ИЗ
	ВТ_РезультатПредварительно КАК Результат

СГРУППИРОВАТЬ ПО
	Результат.Период,
	Результат.КатегорияРС,
	Результат.Бренд,
	Результат.КонтрагентВГО,
	Результат.ЛинияPL
